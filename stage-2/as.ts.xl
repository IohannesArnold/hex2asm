# ########################################################################
# Mnemonic tables

# Fixed sized entries of form
#   struct mnemonic {
#     char name[12];  # Null terminated
#     char type;      # Types 00 to 05, or FF for directives; documented below
#     char params[3]; # Meaning dependent on type; documented below
#   };
#
# List of mnemonics is terminated by an entirely null marker.

mnemonics:

# Type 00 instructions.   Void instructions (i.e. with no arguments), e.g.
#
#   NOP                     90
#
# The three parameters are: 
#   1) the number of bytes in the op-code (01 here),
#   2) the first op-code byte (90), and 
#   3) the second op-code byte (n/a here).

4E 4F 50 00  00 00 00 00  00 00 00 00    00 01 90 00    # NOP
50 55 53 48  46 00 00 00  00 00 00 00    00 01 9C 00    # PUSHF
50 4F 50 46  00 00 00 00  00 00 00 00    00 01 9D 00    # POPF
52 45 50 00  00 00 00 00  00 00 00 00    00 01 F3 00    # REP
52 45 50 45  00 00 00 00  00 00 00 00    00 01 F3 00    # REPE
52 45 50 4E  45 00 00 00  00 00 00 00    00 01 F4 00    # REPNE
4D 4F 56 53  42 00 00 00  00 00 00 00    00 01 A4 00    # MOVSB
43 4D 50 53  42 00 00 00  00 00 00 00    00 01 A6 00    # CMPSB
53 43 41 53  42 00 00 00  00 00 00 00    00 01 AE 00    # SCASB
43 42 57 00  00 00 00 00  00 00 00 00    00 02 66 98    # CBW    [aka CBTW]
43 57 44 45  00 00 00 00  00 00 00 00    00 01 98 00    # CWDE   [aka CWTL]

 

# Type 01 instructions.   A single immediate 8-bit argument, e.g.
#
#   INT     imm8            CD
#
# The three parameters are: 
#   1) the number of bytes in the op-code (01 here),
#   2) the first op-code byte (CD), and 
#   3) the second op-code byte (n/a here).

49 4E 54 00  00 00 00 00  00 00 00 00    01 01 CD 00    # INT



# Type 02 instructions.   A single immediate 32-bit argument, e.g.
#
#   CALL    rel32           E8
#
# The three parameters are: 
#   1) the number of bytes in the op-code (01 here),
#   2) the first op-code byte (E8), and 
#   3) the second op-code byte (n/a here).

43 41 4C 4C  00 00 00 00  00 00 00 00    02 01 E8 00    # CALL
4A 4D 50 00  00 00 00 00  00 00 00 00    02 01 E9 00    # JMP
4A 45 00 00  00 00 00 00  00 00 00 00    02 02 0F 84    # JE
4A 47 00 00  00 00 00 00  00 00 00 00    02 02 0F 8F    # JG
4A 47 45 00  00 00 00 00  00 00 00 00    02 02 0F 8D    # JGE
4A 41 00 00  00 00 00 00  00 00 00 00    02 02 0F 87    # JA
4A 41 45 00  00 00 00 00  00 00 00 00    02 02 0F 83    # JAE
4A 4C 00 00  00 00 00 00  00 00 00 00    02 02 0F 8C    # JL
4A 4C 45 00  00 00 00 00  00 00 00 00    02 02 0F 8E    # JLE
4A 42 00 00  00 00 00 00  00 00 00 00    02 02 0F 82    # JB
4A 42 45 00  00 00 00 00  00 00 00 00    02 02 0F 86    # JBE
4A 4E 45 00  00 00 00 00  00 00 00 00    02 02 0F 85    # JNE
4A 43 00 00  00 00 00 00  00 00 00 00    02 02 0F 82    # JC == JB
4A 4F 00 00  00 00 00 00  00 00 00 00    02 02 0F 80    # JO


# Type 03 instructions.   A single r/m8 operand, e.g.
#
#   INCB    r/m8            FE /0
#
# The three parameters are:
#   1) the number 1 (to allow it to be treated as the opcode length)
#   2) the op-code byte (FE), and 
#   3) the three-bit reg field for the ModR/M byte (0 here).

49 4E 43 42  00 00 00 00  00 00 00 00    03 01 FE 00    # INCB
44 45 43 42  00 00 00 00  00 00 00 00    03 01 FE 01    # DECB
4E 45 47 42  00 00 00 00  00 00 00 00    03 01 F6 03	# NEGB
53 41 4C 42  00 00 00 00  00 00 00 00    03 01 D2 04    # SALB  [%cl]
53 48 4C 42  00 00 00 00  00 00 00 00    03 01 D2 04    # SHLB  [%cl]
53 41 52 42  00 00 00 00  00 00 00 00    03 01 D2 07    # SARB  [%cl]
53 48 52 42  00 00 00 00  00 00 00 00    03 01 D2 05    # SHRB  [%cl]
4D 55 4C 42  00 00 00 00  00 00 00 00    03 01 F6 04	# MULB  [%eax]
49 4D 55 4C  42 00 00 00  00 00 00 00    03 01 F6 05	# IMULB [%eax]
44 49 56 42  00 00 00 00  00 00 00 00    03 01 F6 06	# DIVB  [%eax]
49 44 49 56  42 00 00 00  00 00 00 00    03 01 F6 07	# IDIVB [%eax]


# Type 04 instructions.   One or a pair of op-codes, e.g.
#
#   INCL     r/m32           FF /0
#
# The three parameters are:
#   1) the number 1 (to allow it to be treated as the opcode length)
#   2) the op-code byte (FF), and 
#   3) the three-bit reg field for the ModR/M byte (0 here).

49 4E 43 4C  00 00 00 00  00 00 00 00    04 01 FF 00    # INCL
44 45 43 4C  00 00 00 00  00 00 00 00    04 01 FF 01    # DECL
4E 45 47 4C  00 00 00 00  00 00 00 00    04 01 F7 03	# NEGL
50 55 53 48  4C 00 00 00  00 00 00 00    04 01 FF 06    # PUSHL
50 4F 50 4C  00 00 00 00  00 00 00 00    04 01 8F 00    # POPL
53 41 4C 4C  00 00 00 00  00 00 00 00    04 01 D3 04    # SALL  [%cl]
53 48 4C 4C  00 00 00 00  00 00 00 00    04 01 D3 04    # SHLL  [%cl]
53 41 52 4C  00 00 00 00  00 00 00 00    04 01 D3 07    # SARL  [%cl]
53 48 52 4C  00 00 00 00  00 00 00 00    04 01 D3 05    # SHRL  [%cl]
4D 55 4C 4C  00 00 00 00  00 00 00 00    04 01 F7 04	# MULL  [%eax]
49 4D 55 4C  4C 00 00 00  00 00 00 00    04 01 F7 05	# IMULL [%eax]
44 49 56 4C  00 00 00 00  00 00 00 00    04 01 F7 06	# DIVL  [%eax]
49 44 49 56  4C 00 00 00  00 00 00 00    04 01 F7 07	# IDIVL [%eax]


# Type 05 instructions.   This is unique to LEA
#
#   LEA     r/m32, r32      8D
#
# The sole parameter is the op code itself (8D).

4C 45 41 00  00 00 00 00  00 00 00 00    05 8D 00 00    # LEA



# Type 05 instructions.   These represent a large family of op-codes, e.g.
#
#   MOV     r8,r/m8         88
#   MOV     r32,r/m32       89
#   MOV     r/m8,r8         8A
#   MOV     r/m32,r32       8B
#   MOV     imm8,r/m8       C6 /0
#   MOV     imm32,r/m32     C7 /0
#
# The three parameters are the register op code (88 here), the 
# immediate op code (C6 here), and the three op-code extension bits
# that are in the reg part of the mod-r/m byte (0 here).

4D 4F 56 00  00 00 00 00  00 00 00 00    05 88 C6 00    # MOV
41 44 44 00  00 00 00 00  00 00 00 00    05 00 80 00    # ADD
53 55 42 00  00 00 00 00  00 00 00 00    05 28 80 05    # SUB
43 4D 50 00  00 00 00 00  00 00 00 00    05 38 80 07    # CMP
41 4E 44 00  00 00 00 00  00 00 00 00    05 20 80 04    # AND
4F 52 00 00  00 00 00 00  00 00 00 00    05 08 80 01    # OR
58 4F 52 00  00 00 00 00  00 00 00 00    05 30 80 06    # XOR



# Type FF 'instructions'.  These are actually directives.    
#
#   .hex_bytes
#
# The sole parameter is an unique code used internally to represent the 
# directive.  Each directive is special-cased in the code.

2E 68 65 78  5F 62 79 74  65 73 00 00    FF 00 00 00    # .hex_bytes



# End of table marker -- first byte of name is NULL.
00 00 00 00  00 00 00 00  00 00 00 00    00 00 00 00 
# ########################################################################


####	#  Function: void* getip(void)
	#  A function to load %eip for the calling frame (specifically
	#  the return address) into %eax.  The usual trick of
	#    CALL next_line; next_line: POP $eax
	#  relies on a forward jump, so we need a separate function.
getip:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	8B 45 04		#	MOVL	4(%ebp), %eax
	5D      		#	POP	%ebp
	C3   			#	RET

####    #  Function: bool isws(char)
	#  Tests whether its argument is in [ \t\r\n]

	#  As with many of the functions here, it is turned upside down
	#  so the entry point is in the middle.  This is because unhexl
	#  is limited to jumps up the file.
.L1:
	5D			#	POP	%ebp
	C3			#	RET
ishws:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	8B 45 08		#	MOVL	8(%ebp), %eax
.L1a:
	3C 20			#	CMPB	$0x20, %al	# ' '
	0F 84 .L1		#	JE	.L1
	3C 09			#	CMPB	$0x09, %al	# '\t'
	0F 84 .L1		#	JE	.L1
	31 C0			#	XORL	%eax, %eax
	E9 .L1			#	JMP	.L1

isws:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	8B 45 08		#	MOVL	8(%ebp), %eax
	3C 09			#	CMPB	$0x0D, %al	# '\r'
	0F 84 .L1		#	JE	.L1
	3C 0A			#	CMPB	$0x0A, %al	# '\n'
	0F 84 .L1		#	JE	.L1
	E9 .L1a			#	JMP	.L1a


####	#  Function: bool isidchr(char)
	#  Tests whether its argument is in [0-9A-Za-z_]
.L2:
	31 C0			#	XORL	%eax, %eax
.L3:
	5D			#	POP	%ebp
	C3			#	RET
isidchr:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	8B 45 08		#	MOVL	8(%ebp), %eax
	3C 30			#	CMPB	$0x30, %al	# '0'
	0F 8C .L2		#	JL	.L2
	3C 39			#	CMPB	$0x39, %al	# '9'
	0F 8E .L3		#	JLE	.L3
.L3a:
	3C 41			#	CMPB	$0x41, %al	# 'A'
	0F 8C .L2		#	JL	.L2
	3C 5A			#	CMPB	$0x5A, %al	# 'Z'
	0F 8E .L3		#	JLE	.L3
	3C 5F			#	CMPB	$0x5F, %al	# '_'
	0F 84 .L3		#	JE	.L3
	3C 61			#	CMPB	$0x61, %al	# 'a'
	0F 8C .L2		#	JL	.L2
	3C 7A			#	CMPB	$0x7A, %al	# 'z'
	0F 8E .L3		#	JLE	.L3
	E9 .L2			#	JMP	.L2


####	#  Function: bool isid1chr(char)
	#  Tests whether its argument is in [.A-Za-z_]
isid1chr:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	8B 45 08		#	MOVL	8(%ebp), %eax
	3C 2E			#	CMPB	$0x2E, %al	# '.'
	0F 84 .L3		#	JE	.L3
	E9 .L3a			#	JMP	.L3a


####	#  Function: int xchr(int c)
	#  Tests whether its argument, c, is a character in [0-9A-F], and if 
	#  so, coverts it to an integer; otherwise returns -1.
.L6:
	2C 37			#	SUBB	$0x37, %al	# 'A'-10
.L7:
	5D			#	POP	%ebp
	C3			#	RET
.L4:
	C6 C0 FF		#	MOVB	$-1, %al
	E9 .L7			#	JMP	.L7
.L5:
	2C 30			#	SUBB	$0x30, %al	# '0'
	E9 .L7			#	JMP	.L7
xchr:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	8B 45 08		#	MOVL	8(%ebp), %eax
	3C 30			#	CMPB	$0x30, %al	# '0'
	0F 8C .L4		#	JL	.L4
	3C 39			#	CMPB	$0x39, %al	# '9'
	0F 8E .L5		#	JLE	.L5
	3C 41			#	CMPB	$0x41, %al	# 'A'
	0F 8C .L4		#	JL	.L4
	3C 46			#	CMPB	$0x46, %al	# 'F'
	0F 8E .L6		#	JLE	.L6
	E9 .L4			#	JMP	.L4


####	#  Function: int dchr(int c)
	#  Tests whether its argument, c, is a character in [0-9], and if 
	#  so, coverts it to an integer; otherwise returns -1.
dchr:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	8B 45 08		#	MOVL	8(%ebp), %eax
	3C 30			#	CMPB	$0x30, %al	# '0'
	0F 8C .L4		#	JL	.L4
	3C 39			#	CMPB	$0x39, %al	# '9'
	0F 8E .L5		#	JLE	.L5
	E9 .L4			#	JMP	.L4


####    #  Not a proper function.
        #  Exits program
error:
        BB 01 00 00 00          #       MOVL    $1, %ebx
success:
        B8 01 00 00 00          #       MOVL    $1, %eax   # 1 == __NR_exit
        CD 80                   #       INT     $0x80


####	#  Function:	void writebyte( int c, ofile* of )
	#  Write out character c
writebyte:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	BA 01 00 00 00		#	MOVL	$1, %edx
.L5a:
	53			#	PUSH	%ebx
	8D 4D 08		#	LEAL	8(%ebp), %ecx	# ofile*
	8B 5D 0C		#	MOVL	12(%ebp), $ebx
	01 53 04		#	ADDL	%edx, 4(%ebx)	# inc counter
	8B 1B 			#	MOVL	(%ebx), %ebx	# fd from ofile
	B8 04 00 00 00		#	MOVL	$4, %eax   # 4 == __NR_write
	CD 80			#	INT	$0x80

	5B			#	POP	%ebx
	5D			#	POP	%ebp
	C3			#	RET


####	#  Function:	void writedword( int dw, ofile* of )
	#  Write out dword dw
writedword:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	BA 04 00 00 00		#	MOVL	$4, %edx
	E9 .L5a			#	JMP	.L5a


####	#  Function:	void unread( char c, ifile* f )
	#  Puts c onto the pushback slot
unread:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp

	#  Locate the pback_slot.
	8B 45 0C		#	MOVL	12(%ebp), $eax
	80 78 04 00		#	CMPB	$0, 4(%eax)  # Test has_pback?
	0F 85 error		#	JNE	error

	#  Write to the pback_slot
	C6 40 04 01		#	MOVB	$1, 4(%eax)
	8A 4D 08		#	MOVB	8(%ebp), $cl
	88 48 05		#	MOVB	%cl, 5(%eax)
	5D			#	POP	%ebp
	C3			#	RET
	

####	#  Function:	void readone( char* p, ifile* f ) 
	#  Reads one byte into p which should already be set.
	#  Returns the number of bytes read in %eax.
	#
	#  This function contains the sole read syscall (syscall #3) so we 
	#  can plumb in use of the pushback slot (pback_slot)
.L9:
	BA 01 00 00 00		#	MOVL	$1, %edx
	8B 1B 			#	MOVL	(%ebx), %ebx	# fd from ifile
	B8 03 00 00 00		#	MOVL	$3, %eax	# 3 = __NR_read
	CD 80			#	INT	$0x80
.L9a:
	5B			#	POP	%ebx
	5D			#	POP	%ebp
	C3			#	RET
	
readone:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	53			#	PUSH	%ebx
	8B 4D 08		#	MOVL	8(%ebp), %ecx

	#  Locate the pback_slot.
	8B 5D 0C		#	MOVL	12(%ebp), $ebx
	80 7B 04 00		#	CMPB	$0, 4(%ebx)  # Test has_pback?
	0F 84 .L9		#	JE	.L9

	#  The pback_slot has a value in it
	C6 43 04 00		#	MOVB	$0, 4(%ebx)
	8A 43 05		#	MOVB    5(%ebx), %al
	88 01			#	MOVB	%al, (%ecx)
	B8 01 00 00 00		#	MOVL	$1, %eax
	E9 .L9a			#	JMP	.L9a


####	#  Function:	void readonex( char* p, ifile* f ) 
	#  Reads one byte into p which should already be set (as above);
	#  exits unsuccessfully in case of error reading.
readonex:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp

	FF 75 0C		#	PUSH	12(%ebp)
	FF 75 08		#	PUSH	8(%ebp)
	E8 readone		#	CALL	readone
	5A			#	POP	%edx
	5A			#	POP	%edx

	83 F8 01		#	CMPL	$1, %eax
	0F 85 error		#	JNE	error

	5D			#	POP	%ebp
	C3			#	RET


####	#  Function:	int getone( ifile* in )
	#  Reads a byte into a temporary buffer (which is freed again) and 
	#  returns the byte.  Exits on error or failure to read.
getone:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	31 C9			#	XORL	%ecx, %ecx
	51			#	PUSH	%ecx		# char buf[4];

	FF 75 08		#	PUSH	8(%ebp)
	8D 4D FC		#	LEA	-4(%ebp), %ecx	# char* bufp;
	51			#	PUSH	%ecx

	E8 readonex		#	CALL	readonex
	8A 45 FC		#	MOVB	-4(%ebp), %al

	83 C4 0C		#	ADDL	$12, %esp
	5D			#	POP	%ebp
	C3			#	RET


####	#  Function:	int read_r8( ifile* in )
	#  We've already read a '%'.  Now read an 8-bit register name.  Return
	#    0 => %al, 1 => %cl, 2 => %dl, 3 => %bl
	#    4 => %ah, 5 => %ch, 6 => %dh, 7 => %bh
.L9c:
	89 C8			#	MOVL	%ecx, %eax
	5A			#	POP	%edx		# value
	5A			#	POP	%edx		# ifile
	5D			#	POP	%ebp
	C3			#	RET

.L9b:
	#  Read and parse the second character
	89 4D FC		#	MOVL	%ecx, -4(%ebp)	# value
	E8 getone		#	CALL	getone
	8B 4D FC		#	MOVL	-4(%ebp), %ecx	# value
	80 F8 6C		#	CMPB	$0x6C, %al	# 'l'
	0F 84 .L9c		#	JE	.L9c
	83 C1 04		#	ADDL	$4, %ecx
	80 F8 68		#	CMPB	$0x68, %al	# 'h'
	0F 84 .L9c		#	JE	.L9c
	E9 error		#	JMP	error

read_r8:
	55 			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	31 C9			#	XORL	%ecx, %ecx
	51			#	PUSH	%ecx		# value

	#  Read the first character
	FF 75 08		#	PUSH	8(%ebp)		# ifile
	E8 getone		#	CALL	getone
	8B 4D FC		#	MOVL	-4(%ebp), %ecx	# value

	#  Parse the first character
	80 F8 61		#	CMPB	$0x61, %al	# 'a'
	0F 84 .L9b		#	JE	.L9b
	41			#	INCL	%ecx
	80 F8 63		#	CMPB	$0x63, %al	# 'c'
	0F 84 .L9b		#	JE	.L9b
	41			#	INCL	%ecx
	80 F8 64		#	CMPB	$0x64, %al	# 'd'
	0F 84 .L9b		#	JE	.L9b
	41			#	INCL	%ecx
	80 F8 62		#	CMPB	$0x62, %al	# 'b'
	0F 84 .L9b		#	JE	.L9b
	E9 error		#	JMP	error


####	#  Function:	int skiphws( ifile* );
	#  Skip horizontal whitespace until a non-whitespace character 
	#  which is unread and returned.
skiphws:
	#  The function entry point
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp

	#  Read one byte 
	FF 75 08		#	PUSH	8(%ebp)		# ifile
.L15:
	E8 getone		#	CALL	getone

	#  Is the byte horizontal white space?  If so, loop back
	50			#	PUSH	%eax
	E8 ishws		#	CALL	ishws
	83 F8 00		#	CMPL	$0, %eax
	58			#	POP	%eax
	0F 85 .L15		#	JNE	.L15

	#  Unread the last character read
	50			#	PUSH	%eax
	E8 unread		#	CALL	unread
	58			#	POP	%eax

	#  Stack cleanup and exit
	5A			#	POP	%edx
	5D			#	POP	%ebp
	C3			#	RET
	

####	#  Function:	int read_rm8( int* disp, ifile* in )
	#  Skip whitespace then read an r/m8.  Returns the ModR/M byte
	#  and if a displacement is present, stores it in disp.

read_rm8:
	55 			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp

	# Does it start with an '%'?
	FF 75 0C		#	PUSH	12(%ebp)	# ifile
	E8 skiphws		#	CALL	skiphws
	E8 getone		#	CALL	getone
	3C 25			#	CMPB	$0x25, %al	# '%'
	0F 85 error		#	JNE	error

	# Read the 8-bit register name (ifile is still on stack)
	E8 read_r8		#	CALL	read_r8
	5A			#	POP	%edx

	# Register code is in %al.  |= with 0xC0 as it's a direct register
	80 C8 C0		#	ORB	$0xC0, %al

	5D			#	POP	%ebp
	C3			#	RET

	

####	#  Function:	int get_label( int strlen, int bits, main_frame* p ):
	#  Lookup the text in p->buffer (which has known length strlen),
	#  Convert it to an offset relative to the current instruction
	#  (we're assumed to have written the opcode and any ModR/M bytes,
	#  so the end of instruction happens after writing the number of bits
	#  in the BITS parameter).  Check the offset can be expressed as a
	#  BITS-bit signed (2's-complement) integer.
.L11a:
	5F			#	POP	%edi
	5E			#	POP	%esi
	5B			#	POP	%ebx
	5D			#	POP	%ebp
	C3			#	RET

get_label:
	55 			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	53			#	PUSH	%ebx
	56			#	PUSH	%esi
	57			#	PUSH	%edi

	8B 4D 08		#	MOVL	8(%ebp), %ecx	# strlen
	8B 5D 10		#	MOVL	16(%ebp), %ebx
	8D 73 A0		#	LEA	-96(%ebx), %esi
	8D BB 90 FB FF FF	#	LEA	-1136(%ebx), %edi
.L11:
	83 C7 10		#	ADDL	$16, %edi
	3B 7B F8		#	CMPL	-8(%ebx), %edi
	0F 8D error		#	JGE	error
	51			#	PUSH	%ecx
	56			#	PUSH	%esi
	57			#	PUSH	%edi
	F3			#	REPE
	   A6			#	  CMPSB
	5F			#	POP	%edi
	5E			#	POP	%esi
	59			#	POP	%ecx
	0F 85 .L11		#	JNE	.L14

	# Found it.  Compute the value.
	8B 45 0C		#	MOVL	12(%ebp), %eax
	C6 C1 03		#	MOVB	$3, %cl
	D3 F8			#	SARL	%cl, %eax
	03 83 94 FB FF FF	#	ADDL	-1132(%ebx), %eax #ofile->count
	2B 47 0C		#	SUBL	12(%edi), %eax
	F7 D8			#	NEGL	%eax

	# %eax is the offset.  If bits == 32, don't care about overflow
	83 7D 0C 20		#	CMPL	$32, 12(%ebp)
	0F 84 .L11a		#	JE	.L11a
	
	# Is it >= 2^(bits-1) ?
	8B 4D 0C		#	MOVL	12(%ebp), %ecx
	FF C9			#	DECL	%ecx
	BA 01 00 00 00		#	MOVL	$1, %edx
	D3 E2			#	SALL	%cl, %edx
	39 C2			#	CMPL	%eax, %edx
	0F 8E error		#	JLE	error

	# Or < 2^(bits-1)?
	F7 DA			#	NEG	%edx
	39 C2			#	CMPL	%eax, %edx
	0F 8F error		#	JG	error
	E9 .L11a		#	JMP	.L11a


####	#  Function:	char* read_id( main_frame* p );
	#  The first byte of an identifier is already p->buf[0].  We 
	#  first confirm it isid1chr() and abort if not.  Then read 
	#  until we reach the end of the id and return a pointer to 
	#  the first non-id char.
	#  XXX Clobbers %esi
read_id:
	55 			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp

	#  Was the first byte a valid character for an identifier?
	8B 4D 08		#	MOVL	8(%ebp), %ecx
	FF 71 A0 		#	PUSH	-96(%ecx)
	E8 isid1chr		#	CALL	isid1chr
	59			#	POP	%ecx
	83 F8 00		#	CMPL	$0, %eax
	0F 84 error		#	JE	error

	#  Continue to read an identifier
	8B 45 08		#	MOVL	8(%ebp), %eax
	8D 88 98 FB FF FF	#	LEA	-1128(%eax), %ecx
	51			#	PUSH	%ecx
	8D 48 A0		#	LEA	-96(%eax), %ecx
.L12:
	41			#	INCL	%ecx
	#  We need to check that we're not going to overflow the buffer.  
	#  But in fact, the instruction table limit is 12 characters (incl.
	#  null termination -- i.e. 11 without), so we use that limit.
	8B 45 08		#	MOVL	8(%ebp), %eax
	8D 70 A0		#	LEA	-96(%eax), %esi
	29 CE			#	SUBL	%ecx, %esi
	83 FE F5		#	CMPL	-$11, %esi
	0F 8C error		#	JL	error

	51			#	PUSH	%ecx
	E8 readonex		#	CALL	readonex
	59			#	POP	%ecx
	FF 31			#	PUSH	(%ecx)
	E8 isidchr		#	CALL	isidchr   # XXX preserves %ecx
	5A			#	POP	%edx
	83 F8 00		#	CMPL	$0, %eax
	0F 85 .L12		#	JNE	.L12
	5A			#	POP	%edx

	#  (%ecx) is now something other than lchr.  Return it
	89 C8			#	MOVL	%ecx, %eax
	5D			#	POP	%ebp
	C3			#	RET



####	#  Function:	int read_imm( int bits, main_frame* p );
	#  Skip whitespace and then read an immediate value ($0xXX or $NNN)
	#  Returns the value read or exits if unable to read.
	#  XXX Clobbers %ebx
.L17:
	#  Have we read any digits?  If not, it's an error
	83 7D F4 00		#	CMPL	$0, -12(%ebp)
	0F 84 error		#	JE	error

.L17a:
	#  Unread the last character read
	5B			#	POP	%ebx		# Overwrite 
	FF 75 FC		#	PUSH	-4(%ebp)	#   bufp
	E8 unread		#	CALL	unread

	#  Stack cleanup and exit
	8B 45 F8		#	MOVL	-8(%ebp), %eax	# return val
	83 C4 14		#	ADDL	$20, %esp
	5D			#	POP	%ebp
	C3			#	RET

.L16:
	#  We're reading decimal and the first value is already in the buffer
	FF 75 FC		#	PUSH	-4(%ebp)
	E8 dchr			#	CALL	dchr
	5B			#	POP	%ebx
	3C FF			#	CMPB	$-1, %al
	0F 84 .L17		#	JE	.L17	# done

	#  Add the digit to the current value -- tedious as IMUL only
	#  acts on the accumulator and cannot read immediates.  Test for
	#  overflow as we go along by using the fact that IMUL and ADDL
	#  are both signed operations which set the OF flag on overflow.
	89 C3			#	MOVL	%eax, %ebx
	8B 45 F8		#	MOVL	-8(%ebp), %eax
	B9 0A 00 00 00		#	MOVL	$10, %ecx
	F7 E1 			#	MUL	%ecx, $eax
	0F 80 error		#	JO	error
	01 D8			#	ADDL	%ebx, %eax
	0F 80 error		#	JO	error
	89 45 F8		#	MOVL	%eax, -8(%ebp)
	FF 45 F4		#	INCL	-12(%ebp)

 	#  Read another digit
	E8 readonex		#	CALL	readonex
	E9 .L16			#	JMP	.L16	# loop

	#  XXX  Test bits
.L16a:
	#  Put the byte into the main buffer and read a label
	8A 45 FC		#	MOVB	-4(%ebp), %al
	8B 4D 0C		#	MOVL	12(%ebp), %ecx
	88 41 A0		#	MOVB	%al, -96(%ecx)

	FF 75 0C		#	PUSH	12(%ebp)	# main_frame
	E8 read_id		#	CALL	read_id
	59			#	POP	%ecx

	#  Get ready to unread the last byte back at the end of the function
	8B 08			#	MOVL	(%eax), %ecx
	89 4D FC		#	MOVL	%ecx, -4(%ebp)

	#  Calculate the string length
	8B 4D 0C		#	MOVL	12(%ebp), %ecx
	8D 51 A0		#	LEA	-96(%ecx), %edx
	29 D0			#	SUBL	%edx, %eax

	FF 75 0C		#	PUSH	12(%ebp)	# main_frame
	FF 75 08		#	PUSH	8(%ebp)		# bits
	50			#	PUSH	%eax		# strlen
	E8 get_label		#	CALL	get_label
	59			#	POP	%ecx
	59			#	POP	%ecx
	59			#	POP	%ecx
	89 45 F8		#	MOVL	%eax, -8(%ebp)
	E9 .L17a		#	JMP	.L17a

read_imm:
	#  The function entry point
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	31 C9			#	XORL	%ecx, %ecx
	51			#	PUSH	%ecx		# char buf[4];
	51			#	PUSH	%ecx		# int val = 0;
	51			#	PUSH	%ecx		# int count = 0;

	#  Skip horizontal whitespace
	8B 4D 0C		#	MOVL	12(%ebp), %ecx
	8D 89 98 FB FF FF	#	LEA	-1128(%ecx), %ecx
	51			#	PUSH	%ecx
	E8 skiphws		#	CALL	skiphws

	#  Check it is a '$' marking the start of an immediate;
	#  if not, it must be an identifier
	8D 4D FC		#	LEA	-4(%ebp), %ecx	# char* bufp;
	51			#	PUSH	%ecx
	E8 readonex		#	CALL	readonex
	80 7D FC 24		#	CMPB	$0x24, -4(%ebp)		# '$'
	0F 85 .L16a		#	JNE	.L16a

	#  XXX Factor out this code into a read_int function.

	#  Read another byte (stack already set up).  Is it a '0' then a 'x'?
	E8 readonex		#	CALL	readonex
	80 7D FC 30		#	CMPB	$0x30, -4(%ebp)		# '0'
	0F 85 .L16		#	JNE	.L16

	E8 readonex		#	CALL	readonex
	80 7D FC 78		#	CMPB	$0x78, -4(%ebp)		# 'x'
	E9 .L16			#	JMP	error	# don't support octal

	#  Set number of hex digits (divide number of bits by 4)
	C6 C1 02		#	MOVB	$2, %cl
	D3 7D 08		#	SARL	%cl, 8(%ebp)

.L18:
	#  We know we're going to have a hex number, so start reading
	E8 readonex		#	CALL	readonex
	FF 75 FC		#	PUSH	-4(%ebp)
	E8 xchr			#	CALL	xchr
	5B			#	POP	%ebx
	3C FF			#	CMPB	$-1, %al
	0F 84 .L17		#	JE	.L17	# done

	#  Add the digit to the current value
	C6 C1 04		#	MOVB	$4, %cl
	D3 65 F8		#	SALL	%cl, -8(%ebp)
	01 45 F8		#	ADDL	%eax, -8(%ebp)
	FF 45 F4		#	INCL	-12(%ebp)

	#  Test for overflow: 
	#  have we read more than the expected number of digits?
	8B 45 08		#	MOVL	8(%ebp), %eax
	39 45 F4		#	CMPL	%eax, -12(%ebp)
	0F 8F error		#	JG	error
	E9 .L18			#	JMP	.L17	# loop


####	#  Function:	void write_oc12( int opcode_info, main_frame* )
	#  Uses the second byte in opcode_info to determine how many
	#  bytes to write (1 or 2) and then writes the third and perhaps
	#  fourth byte out.
.L19:
	5B			#	POP	%ebx	# ofile
	5B			#	POP	%ebx
	5D			#	POP	%ebp
	C3			#	RET

write_oc12:
	55			#	PUSH	%ebp
	89 E5			#	MOVL	%esp, %ebp
	53			#	PUSH	%ebx

	#  Find the ofile object
	8B 45 0C		#	MOVL	12(%ebp), %eax
	8D 88 90 FB FF FF	#	LEA	-1136(%eax), %ecx
	51			#	PUSH	%ecx

	#  Unconditionally write the first byte
	8B 55 08		#	MOVL	8(%ebp), %edx
	89 D3			#	MOVL	%edx, %ebx
	C6 C1 10		#	MOVB	$16, %cl
	D3 EB			#	SHRL	%cl, %ebx
	53			#	PUSH	%ebx
	E8 writebyte		#	CALL	writebyte
	5B			#	POP	%ebx

	#  Is there a second byte?
	8B 55 08		#	MOVL	8(%ebp), %edx
	80 FE 01		#	CMPB	$01, %dh
	0F 8E .L19		#	JLE	.L19
	C6 C1 08		#	MOVB	$8, %cl
	D3 EB			#	SHRL	%cl, %ebx
	53			#	PUSH	%ebx
	E8 writebyte		#	CALL	writebyte
	5B			#	POP	%ebx
	E9 .L19			#	JMP	.L19

	
####    #  The main function.
        #  Stack is arranged as follows:
	#
	#	-4(%ebp)	instrct* mnemonics
	#       -8(%ebp)	label* label_end
	#      -12(%ebp)	label* label_end_store
	#      -96(%ebp)	char buffer[80]
	#    -1120(%ebp)	label labels[64]
	#    -1128(%ebp)        ifile stdout
	#    -1136(%ebp)	ofile stdin
	#
	#  where label is a { char name[12]; int addr; },
        #  instrct is a { char name[12]; char type; char data[3]; },
	#  ifile is a { int fd; bool has_pback :8; char pback_char; int:16; }.
	#  and ofile is a { int fd; int count; }.

ret:
	#  This ret is labelled to allow various bits of main to
	#  jump up to it in order to effect a forwards jump.
	31 C0			#	XORL	%eax, %eax
ret1:
	C3			#	RET

	#  --- Test for a comment.
	#  If found, skip over comment line until we've read a LF
	#  At end of section, %eax=1 iff we read a comment.
	#  If %eax=0, all other registers are unaltered.
comment:
        80 7D A0 23             #       CMPB    $0x23, -96(%ebp)        # '#'
	0F 85 ret		#	JNE	ret
	8D 8D 98 FB FF FF	#	LEA	-1128(%ebp), %ecx	# ifile
        51			#	PUSH	%ecx
	8D 4D A0		#	LEA	-96(%ebp), %ecx
	51			#	PUSH	%ecx
.L10:
	E8 readonex		#	CALL	readonex
	80 7D A0 0A		#	CMPB	$0x0A, -96(%ebp)	# '\n'
	0F 85 .L10		#	JNE	.L10
	5A			#	POP	%edx
	5A			#	POP	%edx
	B8 01 00 00 00		#	MOVL	$1, %eax
	C3			#	RET	# to main loop

	#  Parts of the label section
labeldef:
	#  Check that we're not about to over run the label store,
	#  and then store the label
	8D 5D F8		#	LEA	-8(%ebp), %ebx
	8B 3B			#	MOVL	(%ebx), %edi
	8B 45 F4		#	MOVL	-12(%ebp), %eax
	39 C7			#	CMPL	%eax, %edi
	0F 8D error		#	JGE	error
	F3			#	REP
	   A4			#	  MOVSB
	8B 85 94 FB FF FF	#	MOVL	-1132(%ebp), %eax #ofile->count
	8B 3B			#	MOVL	(%ebx), %edi
	89 47 0C		#	MOVL	%eax, 12(%edi)
	83 03 10		#	ADDL	$16, (%ebx)
	B8 01 00 00 00		#	MOVL	$1, %eax
	C3			#	RET	# to main loop

insn_end:
	#  XXX: Should we require only ws or comments until end of line?
	#  XXX: Or support a ';' instruction separator?
	B8 01 00 00 00		#	MOVL	$1, %eax
	E9 ret1			#	JMP	ret1

type_00:
	#  Write out a type 00 instruction.  These are easiest as they
	#  have no parameters.
	55			#	PUSH	%ebp		# main_frame
	52			#	PUSH	%edx	# opcode info
	E8 write_oc12		#	CALL	write_oc12
	5A			#	POP	%edx
	5A			#	POP	%edx
	E9 insn_end		#	JMP	insn_end

type_01:
	#  Write the opcode byte(s).
	55			#	PUSH	%ebp		# main_frame
	52			#	PUSH	%edx	# opcode info
	E8 write_oc12		#	CALL	write_oc12
	5A			#	POP	%edx

	#  Skip ws and read immediate.
	B8 08 00 00 00		#	MOVL	$8, %eax	# bits
	50			#	PUSH	%eax
	E8 read_imm		#	CALL	read_imm
	59			#	POP	%ecx
	59			#	POP	%ecx

	#  Byte is in %al (as part of return from read_imm): write it.
	8D 8D 90 FB FF FF	#	LEA	-1136(%ebp), %ecx	# ofile
        51			#	PUSH	%ecx
	50			#	PUSH	%eax
	E8 writebyte		#	CALL	writebyte
	5B			#	POP	%ebx
	5B			#	POP	%ebx
	E9 insn_end		#	JMP	insn_end

type_02:
	#  Write the opcode byte(s).
	55			#	PUSH	%ebp		# main_frame
	52			#	PUSH	%edx	# opcode info
	E8 write_oc12		#	CALL	write_oc12
	5A			#	POP	%edx

	#  Skip ws and read immediate (or, more likely, label).
	B8 20 00 00 00		#	MOVL	$32, %eax	# bits
	50			#	PUSH	%eax
	E8 read_imm		#	CALL	read_imm
	59			#	POP	%ecx
	59			#	POP	%ecx

	#  Dword is in %eax: write it.
	8D 8D 90 FB FF FF	#	LEA	-1136(%ebp), %ecx	# ofile
        51			#	PUSH	%ecx
	50			#	PUSH	%eax
	E8 writedword		#	CALL	writedword
	5B			#	POP	%ebx
	5B			#	POP	%ebx
	E9 insn_end		#	JMP	insn_end

type_03:
	#  Write the opcode byte(s).
	55			#	PUSH	%ebp		# main_frame
	52			#	PUSH	%edx	# opcode info
	E8 write_oc12		#	CALL	write_oc12
	5A			#	POP	%edx
	59			#	POP	%ecx

	# Read the register
	52			#	PUSH	%edx	# store opcode info
	8D 8D 98 FB FF FF	#	LEA	-1128(%ebp), %ecx	# ifile
        51			#	PUSH	%ecx
	C6 C1 00		#	MOVB	$0, %ecx	# NULL XXX
        51			#	PUSH	%ecx
	E8 read_rm8		#	CALL	read_rm8
	5A			#	POP	%edx
	5A			#	POP	%edx
	5A			#	POP	%edx
	
	#  ModR/M is in %al.  |= with opcode bits (4th byte of %edx) <<3
	89 D3			#	MOVL	%edx, %ebx
	C6 C1 18		#	MOVB	$24, %cl
	D3 EB			#	SHRL	%cl, %ebx
	C6 C1 03		#	MOVB	$3, %cl
	D3 E3			#	SHLL	%cl, %ebx
	08 D8			#	ORB	%bl, %al

	#  Write the ModR/M byte
	8D 8D 90 FB FF FF	#	LEA	-1136(%ebp), %ecx	# ofile
        51			#	PUSH	%ecx
	50			#	PUSH	%eax		# byte
	E8 writebyte		#	CALL	writebyte
	5B			#	POP	%ebx
	5B			#	POP	%ebx
	E9 insn_end		#	JMP	insn_end


hex_bytes:
	#  Skip horizontal whitespace and read a byte
	8D 8D 98 FB FF FF	#	LEA	-1128(%ebp), %ecx	# ifile
        51			#	PUSH	%ecx
	E8 skiphws		#	CALL	skiphws
	8D 4D A0		#	LEA	-96(%ebp), %ecx
	51			#	PUSH	%ecx
	E8 readonex		#	CALL	readonex
	5A			#	POP	%edx
	5A			#	POP	%edx

	# Is it a line ending?  If so, end parsing the directive.
	80 7D A0 0A		#	CMPL	$0x0A, -96(%ebp)	# '\n'
	B8 01 00 00 00		#	MOVL	$1, %eax
	0F 84 ret1		#	JE	ret1	# to main loop

	#  Start parsing an octet
	FF 75 A0		#	PUSH	-96(%ebp)
	E8 xchr			#	CALL	xchr
	5B			#	POP	%ebx
	3C FF			#	CMPB	$-1, %al
	0F 84 error		#	JE	error

	#  Read the next byte
	50			#	PUSH	%eax
	8D 8D 98 FB FF FF	#	LEA	-1128(%ebp), %ecx	# ifile
        51			#	PUSH	%ecx
	8D 4D A1		#	LEA	-95(%ebp), %ecx
	51			#	PUSH	%ecx
	E8 readonex		#	CALL	readonex
	5A			#	POP	%edx
	5A			#	POP	%edx
	5B			#	POP	%ebx	# The first byte

	#  Process it
	FF 75 A1		#	PUSH	-95(%ebp)
	E8 xchr			#	CALL	xchr
	5A			#	POP	%edx
	83 F8 FF		#	CMPL	$-1, %eax
	0F 84 error		#	JE	error
	C6 C1 04		#	MOVB	$4, %cl
	D2 E3			#	SALB	%cl, %bl
	00 D8			#	ADDB	%bl, %al

	#  Byte is in %al; let's write it, and increment the address counter
	8D 8D 90 FB FF FF	#	LEA	-1136(%ebp), %ecx	# ofile
        51			#	PUSH	%ecx
	50			#	PUSH	%eax
	E8 writebyte		#	CALL	writebyte
	5B			#	POP	%ebx
	5B			#	POP	%ebx
	0F 85 hex_bytes		#	JNE	hex_bytes


	#  --- Test for an identifier at top level in the source file.
        #  This might be a label, a mnemonic or a directive.
tl_ident:
	55			#	PUSH	%ebp
	E8 read_id		#	CALL	read_id
	59			#	POP	%ecx
	89 C1			#	MOVL	%eax, %ecx

	#  (%ecx) is now something other than lchr.  Is it a colon?
	#  Also, null terminate, load %esi with start of string, and
	#  %ecx with its length inc. NUL.
	80 39 3A		#	CMPB	$0x3A, (%ecx)        # ':'
	9C			#	PUSHF
	8B 11			#	MOVL	(%ecx), %edx
	C6 01 00		#	MOVB	$0, (%ecx)	# '\0' term.
	41			#	INCL	%ecx
	8D 75 A0		#	LEA	-96(%ebp), %esi
	29 F1			#	SUBL	%esi, %ecx
	83 F9 12		#	CMPL	$12, %ecx
	0F 8F error		#	JG	error
	9D			#	POPF
	0F 84 labeldef		#	JE	labeldef

	#  It must be either a directive or an instruction
	8D 85 98 FB FF FF	#	LEA	-1128(%ebp), %eax	# ifile
	51			#	PUSH	%ecx	# we care about this
        50			#	PUSH	%eax
	52			#	PUSH	%edx
	E8 unread		#	CALL	unread
	59			#	POP	%ecx
	59			#	POP	%ecx
	59			#	POP	%ecx

	#  Look up the mnemonic or directive
	#  %esi points to the null-terminated identifier name
	8B 7D FC		#	MOV	-4(%ebp), %edi
	81 EF 10 00 00 00	#	SUBL	$16, %edi
.L14a:
	83 C7 10		#	ADDL	$16, %edi
	81 3F 00 00 00 00  	#	CMPL	$0, (%edi)
	0F 84 error		#	JE	error
	51			#	PUSH	%ecx
	56			#	PUSH	%esi
	57			#	PUSH	%edi
	F3			#	REPE
	   A6			#	  CMPSB
	5F			#	POP	%edi
	5E			#	POP	%esi
	59			#	POP	%ecx
	0F 85 .L14a		#	JNE	.L14a

	#  Found the mnemonic or directive.  Get it's type (and parameters)
	#  and see whether it's a directive (which must be a .hex_bytes)
	8B 57 0C 		#	MOVL	$12(%edi), %edx
	80 FA FF 		#	CMPB	$FF, %dl
	0F 84 hex_bytes 	#	JE	hex_bytes
	80 FA 00		#	CMPB	$00, %dl
	0F 84 type_00		#	JE	type_00
	80 FA 01		#	CMPB	$01, %dl
	0F 84 type_01		#	JE	type_01
	80 FA 02		#	CMPB	$02, %dl
	0F 84 type_02		#	JE	type_02
	80 FA 03		#	CMPB	$03, %dl
	0F 84 type_03		#	JE	type_03

	#  XXX  write code for the other types of instruction; exit(3) for now
        BB 03 00 00 00          #       MOVL    $3, %ebx
	E9 success		#	JMP	success
	C3			#	RET	# to main loop

	#  --- The main loop
main:
	89 E5			#	MOVL	%esp, %ebp
	81 EC 70 04 00 00	#	SUBL	$1136, %esp
	#  label* label_end = &labels[0];
	8D 85 A0 FB FF FF	#	LEA	-1120(%ebp), %eax
	89 45 F8		#	MOVL	%eax, -8(%ebp)
	#  label* label_end_store = &labels[64];  # 1024 == 64 * sizeof(label)
	81 C0 00 04 00 00	#	ADDL	$1024, %eax
	89 45 F4		#	MOVL	%eax, -12(%ebp)
	#  memset( &stdout, 0, sizeof(ofile) );
	C7 85 90 FB FF FF 01 00 00 00 #	MOVL	$0, -1136(%ebp) # 1 == stdout
	C7 85 94 FB FF FF 00 00 00 00 #	MOVL	$0, -1132(%ebp)
	#  memset( &stdin, 0, sizeof(ifile) );
	C7 85 98 FB FF FF 00 00 00 00 #	MOVL	$0, -1128(%ebp) # 0 == stdin
	C7 85 9C FB FF FF 00 00 00 00 #	MOVL	$0, -1124(%ebp)

	#  Locate the mnemonics table:  instrct* mnemonics = &mnemonics;
	E8 getip		#	CALL	getip
	81 C0 mnemonics		#	ADDL	mnemonics, %eax
	81 C0 06 00 00 00	#	ADDL	$6, %eax   # len of prev instr
	89 45 FC		#	MOVL	%eax, -4(%ebp)

.L8:
	#  Read one byte (not with readonex because EOF is permitted)
	8D 8D 98 FB FF FF	#	LEA	-1128(%ebp), %ecx
        51			#	PUSH	%ecx
	8D 4D A0		#	LEA	-96(%ebp), %ecx
        51			#	PUSH	%ecx
	E8 readone		#	CALL	readone
	5A			#	POP	%edx
	5A			#	POP	%edx
	83 F8 00		#	CMPL	$0, %eax
	0F 8C error		#	JL	error
	89 C3			#	MOVL	%eax, %ebx  # zero exit status
	0F 84 success		#	JE	success

	#  Is the byte white space?  If so, loop back
	8A 45 A0		#	MOVB	-96(%ebp), %al
	50			#	PUSH	%eax
	E8 isws			#	CALL	isws
	83 F8 00		#	CMPL	$0, %eax
	5A			#	POP	%edx
	0F 85 .L8		#	JNE	.L8

	#  We have a byte.  What is it?
	E8 comment		#	CALL	comment
	83 F8 00		#	CMP	$0, %eax
	0F 85 .L8		#	JNE	.L8

	#  or perhaps a top-level identifier (label / mnemonic / directive)
	E8 tl_ident		#	CALL	tl_ident
	83 F8 00		#	CMP	$0, %eax
	0F 85 .L8		#	JNE	.L8

	#  Nothing else is permitted here.	
	E9 error		#	JMP	error


####    #  And finally, the entry point.
        #  Last per requirement for elfify.
        E9 main                 #       JMP     main

